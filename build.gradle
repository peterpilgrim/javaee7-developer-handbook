/*******************************************************************************
 * DO NOT ALTER OR REMOVE COPYRIGHT NOTICES OR THIS HEADER.
 *
 * Copyright (c) 2013,2014 - 2016 by Peter Pilgrim, Milton Keynes, United Kingdom
 *
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the GNU GPL v3.0
 * which accompanies this distribution, and is available at:
 * http://www.gnu.org/licenses/gpl-3.0.txt
 *
 * Developers:
 * Peter Pilgrim -- design, development and implementation
 *               -- Blog: http://www.xenonique.co.uk/blog/
 *               -- Twitter: @peter_pilgrim
 *
 * Contributors:
 *
 *******************************************************************************/

// Packt Publishing and Peter A. Pilgrim present the
// ================================================================================
//    J A V A    E E   7    D E V E L O P E R     H A N D B O O K
// ================================================================================
//
// This Gradle build is the root and does compile any software explicitly
//


apply plugin: 'java'


// The name of this project
project.archivesBaseName = 'javaee7-developer-handbook'

// --------------------------------------------------------------------------------
// Settings for all Java EE 7 projects
// --------------------------------------------------------------------------------
allprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    // Define equivalent Maven GAV coordinates.
    group = 'uk.co.xenonique.javaee7handbook'
    version = '1.0-SNAPSHOT'

    ext {
        glassfishVersion    = "4.1"
        javaeeVersion       = "7.0"
        ozarkVersion        = "1.0.0-m01"
        junitVersion        = "4.12"
        hamcrestVersion     = "1.3"
        mockitoVersion      = "1.10.19"
        shrinkWrapVersion   = "1.0.1"
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url 'https://maven.java.net/content/groups/promoted'
        }
        maven {
            url 'http://repository.jboss.org/nexus/content/groups/public'
        }
    }

    // Java version compatibility to use when compiling Java source 
    sourceCompatibility = '1.8'
    // Java version to generate classes
    targetCompatibility = '1.8'

    task wrapper(type: Wrapper) {
        gradleVersion = '2.10'
    }

    idea {

        module {
            //if you love browsing Javadoc
            downloadJavadoc = false

            //and hate reading sources :)
            downloadSources = true
        }
    }
}



// --------------------------------------------------------------------------------
// Configure settings for Digital Java EE 7 sub projects
// --------------------------------------------------------------------------------

subprojects {
    dependencies {
//        compile     'joda-time:joda-time:2.2'
//        compile     'org.slf4j:slf4j-simple:1.6.4'

//        compile     'org.glassfish.main.extras:glassfish-embedded-all:4.1'
//        compile     'com.javaeehandbook.book1:glassfish-embedded-runner:1.0'
//        compile     'org.jboss.shrinkwrap:shrinkwrap-api:1.0.1'
//        compile     'org.jboss.shrinkwrap:shrinkwrap-impl-base:1.0.1'

        testCompile "org.hamcrest:hamcrest-all:${hamcrestVersion}"
        testCompile "junit:junit:${junitVersion}"
        testCompile "org.mockito:mockito-core:${mockitoVersion}"
    }

    // Override Gradle defaults - a force an exploded JAR view
    sourceSets {
        main {
            output.resourcesDir = 'build/classes/main'
            output.classesDir   = 'build/classes/main'
        }
        test {
            resources {
                srcDir 'src/test/resources'
            }
            resources {
                srcDir 'src/test/resources-glassfish-embedded'
            }
    //        resources {
    //            srcDir 'src/test/resources-jbossas-embedded'
    //        }

            output.resourcesDir = 'build/classes/test'
            output.classesDir   = 'build/classes/test'
        }
    }
}



// --------------------------------------------------------------------------------
// Specific tasks and configuration for the current gradle module
// --------------------------------------------------------------------------------

// Specific task to build the ZIP 
task myZip(type: Zip) {
    description = "Creates a ZIP distribution of the `${archivesBaseName}' project."
    from '.'
    def datestamp = new Date().format("yyyyMMdd")
    def targetName = "EN7942-${archivesBaseName}-sources-${version}-${datestamp}.zip"
    println "**** Creating achive targetName=${targetName} ****"
    archiveName "${targetName}"
    include '**/src/**'
    include '**/*.xml'
    include '**/*.gradle'
    include '**/*.txt'
    include '**/*.md'
    include '**/.gitignore'
    exclude '**/.gradle'
    exclude '**/.gradle/**'
    exclude '**/.idea'
    exclude '**/.idea/**'
    exclude '**/.eclipse'
    exclude '**/.classpath'
    exclude '**/build'
    exclude '**/build/**'
    exclude '**/target'
    exclude '**/target/**'
    exclude '**/classes'
    exclude '**/classes/**'
    exclude '**/out'
    exclude '**/out/**'

    into "EN7942-${archivesBaseName}"

    /*
    exclude {
        details ->
            details.file.name.equals('.gradle')
    }
    */
}


// End.
